apply plugin: 'java'
apply plugin: 'eclipse'

/** Configure all projects. */
allprojects {
    group = 'com.tactfactory.harmony'
    version = '0.7.0'
}

buildscript {
    repositories {
        flatDir name: 'libs', dirs: "./config/plugins"
    }
}

project(':vendor') {
    /** Configure all bundles. */
    subprojects {
        apply plugin: 'java'
        apply plugin: 'maven'
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        
        repositories {
            mavenCentral()
        }

        dependencies {
            /** Add all jar from lib directory to classpath. */
            compile fileTree(dir: 'lib', include: '*.jar')
        }

        sourceSets {
            main {
                java {
                    srcDir 'src'
                    exclude '**/test/**'
                }
            }
            test {
                java {
                    srcDir 'src'
                    include '**/test/**'
                }
            }
        }

        /** Test SuiteClasses. */
        test {
            include '**/test/AllTests*'
        }
        
        clean << {
            FileTree tree = fileTree('./').include('*.jar')
            tree.visit {element ->
                delete element.file
            }
        }
    }
    
    /** Apply configuration for all bundles except 'tact-core'. */
    configure(subprojects.findAll {it.name != 'tact-core'}) {
        dependencies {
            //Add tact-core source to dependencies
            runtime files('../tact-core/harmony.jar')
            compile project(':vendor:tact-core')
            
            //Add tact-core source tests to dependencies
            testCompile project(':vendor:tact-core').sourceSets.test.output
        }
    }
}

dependencies {
    /** Add all jar from lib directory of all bundles to classpath. */
    compile fileTree(dir: 'vendor', include: '**/lib/*.jar')
}

sourceSets {
    main {
        java {
            srcDirs = project(':vendor').subprojects.collect {
                new File(it.projectDir, 'src')
            }
        }
    }
}

apply from: 'config/plugins/jar.gradle'
apply from: 'config/plugins/javadoc.gradle'
apply from: 'config/plugins/jacoco.gradle'
apply from: 'config/plugins/reports.gradle'
