apply plugin: 'java'
apply plugin: 'eclipse'

/** Configure all projects. */
allprojects {
    group = 'com.tactfactory.harmony'
    version = '0.6.4'
}

buildscript {
    repositories {
        flatDir name: 'libs', dirs: "./config/plugins"
    }
    dependencies {
        classpath 'com.tactfactory:emma:1.0'
    }
}

project(':vendor') {
    /** Configure all bundles. */
    subprojects {
        apply plugin: 'java'
        apply plugin: 'maven'
        
        repositories {
            mavenCentral()
        }

        dependencies {
            /** Add all jar from lib directory to classpath. */
            compile fileTree(dir: 'lib', include: '*.jar')
        }

        sourceSets {
            main {
                java {
                    srcDir 'src'
                    exclude '**/test/**'
                }
            }
            test {
                java {
                    srcDir 'src'
                    include '**/test/**'
                }
            }
        }
    }
    
    /** Apply configuration for all bundles except 'tact-core'. */
    configure(subprojects.findAll {it.name != 'tact-core'}) {
        dependencies {
            //Add tact-core source to dependencies
            compile project(':vendor:tact-core')
            //Add tact-core source tests to dependencies
            testCompile project(':vendor:tact-core').sourceSets.test.output
        }
    }
}

apply from: 'config/plugins/jar.gradle'
apply from: 'config/plugins/javadoc.gradle'
apply from: 'config/plugins/emma.gradle'
apply from: 'config/plugins/reports.gradle'
apply from: 'config/plugins/eclipse.gradle'

/*dependencies {
    runtime project(':vendor:tact-core')
    runtime project(':vendor:tact-rest')
}*/
