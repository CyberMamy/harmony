/** Configure all projects. */
allprojects {
    group = 'com.tactfactory.harmony'
    version = '0.8.0'

    repositories {
        jcenter()
    }
}

buildscript {
    repositories {
        flatDir name: 'libs', dirs: "./config/plugins"
    }
}

apply plugin: 'java'
apply from: 'config/plugins/eclipse.gradle'

repositories {
    jcenter()
}

project(':vendor') {
    /** Configure all bundles. */
    subprojects {
        apply plugin: 'java'
        apply plugin: 'maven'
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        repositories {
            jcenter()
        }

        dependencies {
            /** Add all jar from lib directory to classpath. */
            compile fileTree(dir: 'lib', include: '*.jar')
        }

        sourceSets {
            main {
                java {
                    srcDir 'src'
                    exclude '**/test/**'
                }
            }
            test {
                java {
                    srcDir 'src'
                    include '**/test/**'
                }
            }
        }

        /** Test SuiteClasses. */
        test {
            include '**/test/AllTests*'
        }

        test.dependsOn jar

        clean << {
            FileTree tree = fileTree('./').include('*.jar')
            tree.visit {element ->
                delete element.file
            }
        }
    }

    /** Apply configuration for all bundles except 'tact-core'. */
    configure(subprojects.findAll {it.name != 'tact-core'}) {
        dependencies {
            //Add tact-core source to dependencies
            runtime files('../tact-core/harmony.jar')
            compile project(':vendor:tact-core')

            //Add tact-core source tests to dependencies
            testCompile project(':vendor:tact-core').sourceSets.test.output
        }
    }
}

tasks.all {
    task ->
        if ((task.name.equals('compileJava'))) {
            task.enabled = false
        }
}

apply from: 'config/plugins/jar.gradle'
apply from: 'config/plugins/javadoc.gradle'
apply from: 'config/plugins/jacoco.gradle'
apply from: 'config/plugins/reports.gradle'
